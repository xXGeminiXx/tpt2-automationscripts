:name DW.Factory:producers_gems
:budget_cap max

; Producer set B: gems + exotic gems.

:const int MAX_TIER 10

:global int dw_run
:global int dw_fac_gen
:global int dw_fac_state
:global int dw_fac_tier
:global double dw_fac_need
:global int dw_fac_err
:global double dw_fac_err_until
:global string dw_fac_task
:global string dw_fac_fail

:local int t
:local int my_gen

wakeup()

start:
my_gen = dw_fac_gen
t = if(t < 1, 1, if(t > MAX_TIER, 1, t))

loop:
gotoif(end, my_gen != dw_fac_gen || dw_run == 0)
dw_fac_tier = t

gotoif(do_gems, count("producer.gems", t) < 1.0)
gotoif(do_exotic, count("producer.exoticgems", t) < 1.0)
goto(next_tier)

next_tier:
t = if(t >= MAX_TIER, 1, t + 1)
waitframe()
goto(loop)

do_gems:
dw_fac_state = 811
dw_fac_task = "Crafting producer: Gems"
dw_fac_need = 1.0 - count("producer.gems", t)
dw_fac_fail = "Missing unlock/materials for producer.gems"
dw_fac_err = 0
craft("producer.gems", t, 1.0)
gotoif(craft_fail, count("producer.gems", t) < 1.0)
dw_fac_fail = ""
goto(next_tier)

do_exotic:
dw_fac_state = 821
dw_fac_task = "Crafting producer: Exotic Gems"
dw_fac_need = 1.0 - count("producer.exoticgems", t)
dw_fac_fail = "Missing unlock/materials for producer.exoticgems"
dw_fac_err = 0
craft("producer.exoticgems", t, 1.0)
gotoif(craft_fail, count("producer.exoticgems", t) < 1.0)
dw_fac_fail = ""
goto(next_tier)

craft_fail:
dw_fac_err = 3
dw_fac_err_until = now() + 10000000.
goto(next_tier)

end:
