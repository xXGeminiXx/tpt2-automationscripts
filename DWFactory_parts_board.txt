:name DW.Factory:parts_board
:budget_cap max

; Refinery chain B: circuit plate.

:const int MAX_TIER 10
:const double TGT_BOARD 1200.0

:global int dw_run
:global int dw_fac_gen
:global int dw_fac_state
:global int dw_fac_tier
:global double dw_fac_need
:global int dw_fac_err
:global double dw_fac_err_until
:global string dw_fac_task
:global string dw_fac_fail

:local int t
:local int my_gen
:local double need

wakeup()

start:
my_gen = dw_fac_gen
t = if(t < 1, 1, if(t > MAX_TIER, 1, t))

loop:
gotoif(end, my_gen != dw_fac_gen || dw_run == 0)
dw_fac_tier = t

gotoif(do_board, count("plate.circuit", t) < TGT_BOARD)
goto(next_tier)

next_tier:
t = if(t >= MAX_TIER, 1, t + 1)
waitframe()
goto(loop)

do_board:
dw_fac_state = 251
dw_fac_task = "Making circuit plates via Refinery"
dw_fac_fail = "Refinery busy"
gotoif(busy, active("refinery"))
need = min(170.0, TGT_BOARD - count("plate.circuit", t))
need = min(need, count("plate", t))
dw_fac_need = need
dw_fac_fail = "Need plates"
gotoif(no_input, need <= 0.)
dw_fac_err = 0
dw_fac_fail = ""
produce("plate", t, need, "refinery")
goto(next_tier)

busy:
dw_fac_err = 1
dw_fac_err_until = now() + 10000000.
goto(next_tier)

no_input:
dw_fac_err = 2
dw_fac_err_until = now() + 10000000.
goto(next_tier)

end:
